import guigen;

extension template::GenericExtensions;

/////////////////////////////////////////////////
// Check indipendenti dalla piattaforma target //
/////////////////////////////////////////////////

// validazioni generali (prodotto/componente etc..)
context GUIModel ERROR "E'necessario specificare il codice prodotto (max 10 caratteri)" :
	codProdotto!=null && codProdotto.length<=10;

context GUIModel ERROR "E'necessario specificare il codice componente (max 20 caratteri)" :
	codComponente!=null && codComponente.length<=20;

context GUIModel ERROR "E'necessario specificare la versione del prodotto nel formato major.minor.bugfix" :
	versioneProdotto!=null && isVersioneFormalmenteCorretta(versioneProdotto);

context GUIModel ERROR "E'necessario specificare la versione del componente nel formato major.minor.bugfix" :
	versioneComponente!=null && isVersioneFormalmenteCorretta(versioneComponente);
	
///// Validazione su pannelli

context ContentPanel ERROR "Il nome dei content panel deve essere univoco all'interno dell'applicazione" :
	((ApplicationArea)this.eContainer).contentPanels.select(cp|cp.name==this.name).size==1; 

context Panel ERROR "E'necessario impostare l'attributo 'name' del pannello" :
	this.name != null && this.name != "";

context Panel ERROR "il nome di un pannello deve essere univoco all'intenro di un content panel" :
    findParentContentPanel(this).eAllContents.typeSelect(Panel).select(p|p.name==this.name).size==1;
	

context ApplicationArea ERROR "Deve essere definito almeno un contentPanel" :
	this.contentPanels!=null && this.contentPanels.size>0;
	
context FormPanel ERROR "E'necessario impostare lo specificatore del layout NSEWC per tutti i widget e tutti i sottopannelli" :
    this.layout.metaType!=UDLRCPanelLayout ||
    ((this.layout.metaType==UDLRCPanelLayout && this.widgets.select(w|w.layoutSpec==null).size==0))
    &&(this.layout.metaType==UDLRCPanelLayout && this.subpanels.select(w|w.layoutSpec==null).size==0)
    ;

context TabSetPanel ERROR "Un tab set panel deve avere almeno un tab" :
	this.tabs.size>0;
	
///// Validazione su widget

context Widget ERROR "E'necessario impostare l'attributo 'name' del widget" :
	this.name != null && this.name != "";

context Widget ERROR "il name dei widgets in un content panel (eccettuati singoli radiobutton) deve essere univoco" :
     this.metaType==RadioButton ||
     findAllWidgetsInContentPanel(findParentContentPanel(this)).select(w|((Widget)w).name==this.name).size==1;
     
context Table ERROR "Non è stato impostato lo schema delle colonne" :
this.columnModel!=null;

context ColumnModel ERROR "Lo schema delle colonne deve contenere almeno una colonna" :
columns != null && columns.size>0;
 	

//// Validazione su azioni e listener

context MenuItem ERROR "Un menu item può generare solo eventi di tipo CLICKED" :
	this.eventHandler==null || this.eventHandler.eventType == EventTypes::CLICKED;

context EventHandler ERROR "Un action Listener deve innescare un'azione a fronte di un tipo di evento" :
	this.action!=null;
	
context JumpAction ERROR "E'necessario impostare il pannello target di una Jump Action" :
	this.jumpTo != null;
	
context ShowDialogAction ERROR "E'necessario impostare il dialog target di una Show Dialog Action" :
    this.dialog != null;
    
context ExecAction ERROR "Una ExecAction deve avere almeno un ActionResult associato" :
	this.results!=null && this.results.size>0;

context ExecAction ERROR "E'necessario specificare il nome del metodo associato all'esecuzione" :
	this.methodName!=null && this.methodName.length>0;

context ApplicationData ERROR "E' necessario specificare il nome dell Application Data" :
	this.name!=null && this.name.length>0;

context ApplicationData ERROR "E' necessario specificare il tipo dell Application Data" :
	this.type!=null;
	
context AppDataBinding ERROR "E'necessario specificare il dato associato ad un DataBinding" :
	this.appData!=null;

context DataWidget ERROR "E'ncessario specificare il tipo del Widget" :
	this.dataType!=null;

context DataWidget ERROR "Il tipo del widget non corrisponde con il tipo del databinding" :
	(this.dataType==null 
	 || this.databinding == null 
	 || (this.databinding!=null && this.databinding.appData==null)
	 || (this.databinding!=null && this.databinding.appData!=null && this.databinding.appData.type==null))
	 || (this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null 
	 	&& ""!=(this.databinding.path))
	 || (
	 	this.dataType!=null 
	 	&& this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null
	 	&& this.dataType==this.databinding.appData.type
	 );

context DataWidget WARNING "Non è possibile verificare a priori la corrispondenza di tipo tra widget e binding "+
	"poichè è impostato l'attributo path: verificare manualmente" :
	(this.dataType==null 
	 || this.databinding == null 
	 || (this.databinding!=null && this.databinding.appData==null)
	 || (this.databinding!=null && this.databinding.appData!=null && this.databinding.appData.type==null))
	 || (this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null 
	 	&& ""==(this.databinding.path))
	 || (
	 	this.dataType!=null 
	 	&& this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null
	 	&& this.dataType==this.databinding.appData.type
	 );	 
	
context ActionResult ERROR "Una ActionResult deve avere associata una azione effettiva" :  
	this.action != null;
	
context SequenceAction ERROR "Una SequenceAction deve prevedere almeno uno step nella sequenza di azioni" :
	this.actions!= null && this.actions.size>0;

context SequenceAction ERROR "Solo l'ultimo step di una SequenceAction può essere una JumpAction" :
	(this.actions.select(a|a.metaType == JumpAction).size==0) ||
	  (this.actions.select(a|a.metaType == JumpAction).size==1) && (this.actions.last().metaType == JumpAction)
	;

context SequenceAction ERROR "Solo l'ultimo step di una SequenceAction può essere una JumpBackAction" :
	(this.actions.select(a|a.metaType == JumpBackAction).size==0) ||
	  (this.actions.select(a|a.metaType == JumpBackAction).size==1) && (this.actions.last().metaType == JumpBackAction);

//context SequenceAction ERROR "Solo l'ultimo step di una SequenceAction può essere una ShowDialogAction" :
//	(this.actions.select(a|a.metaType == ShowDialogAction).size==0) ||
//	  (this.actions.select(a|a.metaType == ShowDialogAction).size==1) && (this.actions.last().metaType == ShowDialogAction);

context ActionOnWidgets ERROR "Le azioni che hanno come oggeto un widget devono essere associate ad almeno un widget" :
	this.targetWidgets != null && this.targetWidgets.size>0;
	
context CommandWidget ERROR "ad un CommandWidget è possibile associare solo la gestione di un evento CLICKED" :
	this.eventHandlers.select(eh|eh.eventType==EventTypes::CLICKED).size==(this.eventHandlers.size);