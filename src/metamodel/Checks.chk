import guigen;

extension org::openarchitectureware::util::stdlib::naming;
extension template::GenericExtensions;
extension template::struts2::Struts2Extensions;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Check indipendenti dalla piattaforma target                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// validazioni generali (prodotto/componente etc..)
context GUIModel ERROR "E'necessario specificare il codice prodotto (max 10 caratteri)" :
	codProdotto!=null && codProdotto.length<=10;

context GUIModel ERROR "E'necessario specificare il codice componente (max 20 caratteri)" :
	codComponente!=null && codComponente.length<=20;

context GUIModel ERROR "E'necessario specificare la versione del prodotto nel formato major.minor.bugfix" :
	versioneProdotto!=null && isVersioneFormalmenteCorretta(versioneProdotto);

context GUIModel ERROR "E'necessario specificare la versione del componente nel formato major.minor.bugfix" :
	versioneComponente!=null && isVersioneFormalmenteCorretta(versioneComponente);


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Validazione su pannelli

context ContentPanel ERROR "Il nome dei content panel deve essere univoco all'interno dell'applicazione" :
	((ApplicationArea)this.eContainer).contentPanels.select(cp|cp.name==this.name).size==1; 

context Panel ERROR "E'necessario impostare l'attributo 'name' del pannello" :
	this.name != null && this.name != "";

context Panel ERROR "il nome di un pannello deve essere univoco all'interno di un ContentPanel" :
    findParentContentPanel(this).eAllContents.typeSelect(Panel).select(p|p.name==this.name).size==1;
	

context ApplicationArea ERROR "Deve essere definito almeno un ContentPanel" :
	this.contentPanels!=null && this.contentPanels.size>0;

// [DM]
context FormPanel ERROR "E' necessario impostare il layout su ogni FormPanel " + loc() :
	this.layout != null;

context FormPanel ERROR "E' necessario impostare lo specificatore del layout NSEWC per tutti i widget e tutti i sottopannelli" :
    (this.layout.metaType!=UDLRCPanelLayout) 
    ||
    (
    	(
    		this.layout.metaType==UDLRCPanelLayout 
    		&& 
    		this.widgets.select(w|w.layoutSpec==null).size==0
    		&&
    		this.widgets.select(w|w.layoutSpec.metaType!=UDLRCWidgetLayoutSpec).size==0
    	)
    	
    	&&
    	(
    		this.layout.metaType==UDLRCPanelLayout 
    		&& 
    		this.subpanels.select(w|w.layoutSpec==null).size==0
    		&&
    		this.subpanels.select(w|w.layoutSpec.metaType!=UDLRCWidgetLayoutSpec).size==0
    	)
    );

context FormPanel ERROR "Il pannello ha il layout 'Grid': e' necessario impostare la posizione in griglia per tutti i widget e i sottopannelli" :
	this.layout.metaType!=GridPanelLayout 
	||
    (
    	(
    		this.layout.metaType==GridPanelLayout 
    		&& 
    		this.widgets.select(w|w.layoutSpec==null).size==0
    		&&
    		this.widgets.select(w|w.layoutSpec.metaType!=GridWidgetLayoutSpec).size==0
    	)
    	&&
    	(
    		this.layout.metaType==GridPanelLayout 
    		&& 
    		this.subpanels.select(w|w.layoutSpec==null).size==0
    		&&
    		this.subpanels.select(p|p.layoutSpec.metaType!=GridWidgetLayoutSpec).size==0
    	)
    );

context TabSetPanel ERROR "Un tab set panel deve avere almeno un tab" :
	this.panels.size>0;

context MultiPanel ERROR "Un MultiPanel deve avere almeno un pannello interno" :
	this.panels.size>0;
	
context MultiPanel ERROR "Un multipanel può contenere solo FormPanel come pannelli interni" :
	this.panels.select(p|!p.metaType.isAssignableFrom(FormPanel)).size==0;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Validazioni su struttura menu
context Menu ERROR "Un menu deve avere almeno un menu item" :
	this.item!=null && this.item.size>0;

context MenuItem WARNING "il menuItem "+this.name+" non ha nessun EventHandler associato ed è quindi inattivo" :
	this.eventHandler!=null; 	


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Validazione su widget

context Widget ERROR "E'necessario impostare l'attributo 'name' del widget" :
	this.name != null && this.name != "";

context Widget ERROR "il name dei widgets in un content panel (eccettuati singoli radiobutton) deve essere univoco" :
     this.metaType==RadioButton ||
     findAllWidgetsInContentPanel(findParentContentPanel(this)).select(w|((Widget)w).name==this.name).size==1;
     
context Table ERROR "Non è stato impostato lo schema delle colonne" :
	this.columnModel!=null;

context ColumnModel ERROR "Lo schema delle colonne deve contenere almeno una colonna" :
	columns != null && columns.size>0;
 
// [DM] una colonna non può essere contemporaneamente editabile e cliccabile
context Column ERROR "Una colonna non puo' essere contemporaneamente editabile e cliccabile" :
!(this.editable && this.eventActive);
 
// [DM] HiddenValue puo' essere inserito solo in un CommandPanel (per il momento...): questo per non complicare le cose con i layout
context HiddenValue ERROR "Il widget HiddenValue puo' essere inserito solo in un FormPanel con Vertical o Horizontal layout" :
 	((Panel)this.eContainer).layout.metaType == VerticalFlowPanelLayout || ((Panel)this.eContainer).layout.metaType == HorizontalFlowPanelLayout;
 

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// Validazione su azioni e listener

context MenuItem ERROR "Un menu item può generare solo eventi di tipo CLICKED" :
	this.eventHandler==null || this.eventHandler.eventType == EventTypes::CLICKED;

context EventHandler ERROR "Un action Listener deve innescare un comando a fronte di un tipo di evento" :
	this.command!=null;
	
context JumpCommand ERROR "E'necessario impostare il pannello target di un Jump Command" :
	this.jumpTo != null;
	
context ShowDialogCommand ERROR "E'necessario impostare il dialog target di uno ShowDialogCommand" :
    this.dialog != null;
    
context ExecCommand ERROR "Un ExecCommand deve avere almeno un CommandOutcome associato" :
	this.results!=null && this.results.size>0;

context ExecCommand ERROR "E'necessario specificare il nome del metodo associato all'esecuzione" :
	this.methodName!=null && this.methodName.length>0;

context ActivateMultiPanelItemCommand ERROR "E'necessario specificare il multi panel" :
	this.multipanel!=null;
		
context ActivateMultiPanelItemCommand ERROR "Il sottopannello (activeItem) non è un item del MultiPanel specificato" :
	this.activeItem==null || 
	this.multipanel.panels.select(p|p==this.activeItem).size>0;

context JumpExtCommand ERROR "E'necessario specificare un url statico o un application data a cui puntare a runtime" :
	this.staticUrl!=null && this.runtimeUrlProvider==null 
	||
	(this.staticUrl==null || this.staticUrl.length==0) && this.runtimeUrlProvider!=null;

context JumpExtCommand ERROR "E'necessario fornire un codice identificativo/descrittivo dell'url esterno a cui saltare" :
	this.locationCode!=null && this.locationCode.length>0;
	
context JumpExtCommand ERROR "JumpExtCommand può utilizzare solo un appData di tipo string come runtime url provider" :
	this.runtimeUrlProvider==null ||
	this.runtimeUrlProvider.type.metaType==SimpleType &&
	((SimpleType)(this.runtimeUrlProvider.type)).code==guigen::SimpleTypeCodes::STRING;

context ApplicationData ERROR "E' necessario specificare il nome dell Application Data" :
	this.name!=null && this.name.length>0;

context ApplicationData ERROR "E' necessario specificare il tipo dell Application Data" :
	this.type!=null;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// constraints su azioni

// un menu view non può avere event handler associati (eredita quelli del menu vero e proprio di cui
// è segnaposto
context MenuView ERROR "ad una MenuView non è possibile associare evnet handler, in quanto eredita gli event handler"+
						" della struttura menu globale (di cui funge solo da segnaposto)" :
	this.eventHandlers.size==0;

// un Treeview può avere solo un event handler di tipo click
context TreeView ERROR "ad una TreeView è possibile associare solo la gestione di un evento CLICKED" :
	this.eventHandlers.select(eh|eh.eventType==EventTypes::CLICKED).size==(this.eventHandlers.size);

// un CommandWidget può avere solo un event handler di tipo click
context CommandWidget ERROR "ad un CommandWidget è possibile associare solo la gestione di un evento CLICKED" :
	this.eventHandlers.select(eh|eh.eventType==EventTypes::CLICKED).size==(this.eventHandlers.size);


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// constraints su binding
	
context AppDataBinding ERROR "E'necessario specificare il dato associato ad un DataBinding" :
	this.appData!=null;

context TreeView ERROR "La fonte dati di un TreeView può solo essere un application data di tipo TreeNode e invece è "+this.multiDataBinding.appData.type :
	this.multiDataBinding==null ||
	(this.multiDataBinding.appData!=null 
	 && 
	 this.multiDataBinding.appData.type.metaType==SimpleType 
	 &&
	 ((SimpleType)this.multiDataBinding.appData.type).code==SimpleTypeCodes::TREE_NODE
	 );
	
context DataWidget ERROR "E'ncessario specificare il tipo del Widget" :
	this.dataType!=null;

context DataWidget ERROR "Il tipo del widget non corrisponde con il tipo del databinding" :
	(this.dataType==null 
	 || this.databinding == null 
	 || (this.databinding!=null && this.databinding.appData==null)
	 || (this.databinding!=null && this.databinding.appData!=null && this.databinding.appData.type==null))
	 || (this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null 
	 	&& ""!=(this.databinding.path))
	 || (
	 	this.dataType!=null 
	 	&& this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null
	 	&& this.dataType==this.databinding.appData.type
	 );

context DataWidget WARNING "Non è possibile verificare a priori la corrispondenza di tipo tra widget e binding "+
	"poichè è impostato l'attributo path: verificare manualmente " :
	(this.dataType==null 
	 || this.databinding == null 
	 || (this.databinding!=null && this.databinding.appData==null)
	 || (this.databinding!=null && this.databinding.appData!=null && this.databinding.appData.type==null))
	 || (this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null 
	 	&& ""==(this.databinding.path))
	 || (
	 	this.dataType!=null 
	 	&& this.databinding!=null 
	 	&& this.databinding.appData!=null 
	 	&& this.databinding.appData.type!=null
	 	&& this.dataType==this.databinding.appData.type
	 );	 
	
context CommandOutcome ERROR "Un CommandOutcome deve avere associata un comando effettivo" :  
	this.command != null;
	
context SequenceCommand ERROR "Una SequenceCommand deve prevedere almeno uno step nella sequenza di comandi" :
	this.commands!= null && this.commands.size>0;

context SequenceCommand ERROR "Solo l'ultimo step di un SequenceCommand può essere un JumpCommand" :
	(this.commands.select(a|a.metaType == JumpCommand).size==0) ||
	  (this.commands.select(a|a.metaType == JumpCommand).size==1) && (this.commands.last().metaType == JumpCommand)
	;

context SequenceCommand ERROR "Solo l'ultimo step di un SequenceCommand può essere un JumpExtCommand" :
	(this.commands.select(a|a.metaType == JumpExtCommand).size==0) ||
	  (this.commands.select(a|a.metaType == JumpExtCommand).size==1) && (this.commands.last().metaType == JumpExtCommand)
	;

context SequenceCommand ERROR "Solo l'ultimo step di un SequenceCommand può essere un JumpBackCommand" :
	(this.commands.select(a|a.metaType == JumpBackCommand).size==0) ||
	  (this.commands.select(a|a.metaType == JumpBackCommand).size==1) && (this.commands.last().metaType == JumpBackCommand);

//context SequenceAction ERROR "Solo l'ultimo step di una SequenceAction può essere una ShowDialogAction" :
//	(this.actions.select(a|a.metaType == ShowDialogAction).size==0) ||
//	  (this.actions.select(a|a.metaType == ShowDialogAction).size==1) && (this.actions.last().metaType == ShowDialogAction);

context CommandOnWidgets ERROR "I comandi che hanno come oggeto un widget devono essere associati ad almeno un widget" :
	this.targetWidgets != null && this.targetWidgets.size>0;

// [DM] check su CommandPanel
// Il CommanPanel è una sottoclasse di FormPanel con alcuni constraints aggiuntivi:
// 1) deve essere contenuto in un FormPanel (es il panel master con layout UDLRC)
// 2) può contenere solo CommandWidget (o al massimo HiddenValue)
// 3) non può contenere sottopannelli
// 4) può avere solo due tipi di layout: [a] horizontal flow, [b] UDLRC ristretto (solo L ed R, tipico delle barre di navigazione)
// 5) non deve avere label (*da verificare*) 
// 1
context CommandPanel ERROR "Un CommandPanel deve essere contenuto in un FormPanel " + loc() :
	this.eContainer.metaType == FormPanel;
// 2
context CommandPanel ERROR "Un CommandPanel può contenere solo CommandWidget o HiddenValue" + loc() :
	commandPanelCommandWidgetCheck(this);
// 3
context CommandPanel ERROR "Un CommandPanel non può contenere sottopannelli " + loc() :
	this.subpanels.size == 0;
// 4
context CommandPanel ERROR "Un CommandPanel può avere solo due tipi di layout: [a] HorizontalFlowPanelLayout, [b] UDLRCPanelLayout ristretto (solo LEFT ed RIGHT) " + loc() :
	commandPanelLayoutCheck(this); // TODO: implementare
// 5
context CommandPanel ERROR "Un CommandPanel non deve avere label " + loc() :
	isNullOrEmpty(this.label); // TODO: verificare se è corretto che sia così

// [DM] check su MenuPanel
// Il MenuPanel è una sottoclasse di FormPanel con alcuni constraints aggiuntivi:
// 1) deve essere contenuto in un FormPanel (es il panel master con layout UDLRC)
// 2) può contenere solo widget di tipo MenuView o TreeView
// 3) non può contenere sottopannelli
// 4) può avere solo un layout Vertical
// 5) non deve avere label (*da verificare*)
// 1
context MenuPanel ERROR "Un MenuPanel deve essere contenuto in un FormPanel " + loc() :
	this.eContainer.metaType == FormPanel;
// 2
context MenuPanel ERROR "Un MenuPanel può contenere solo widget di tipo MenuView o TreeView " + loc() :
	menuPanelWidgetCheck(this);
// 3
context MenuPanel ERROR "Un MenuPanel non può contenere sottopannelli " + loc() :
	this.subpanels.size == 0;
// 4
context MenuPanel ERROR "Un MenuPanel può avere solo un layout di tipo VerticalFlowPanelLayout " + loc() :
	this.layout.metaType == VerticalFlowPanelLayout; // TODO: implementare
// 5
context MenuPanel ERROR "Un MenuPanel non deve avere label " + loc() :
	isNullOrEmpty(this.label); // TODO: verificare se è corretto che sia così
	
	
////////////////////////////////////////////////////////////////////////////////////////
/// constraints su security model
////////////////////////////////////////////////////////////////////////////////////////

context UISecurityConstraint ERROR "Non è possibile specificare constraint di AUTORIZZAZIONE "+
"se non è specificato un metodo di AUTENTICAZIONE" :
	((GUIModel)this.eRootContainer).securityModel.autenticationMethod != null;
	
context Actor ERROR "E'necessario specificare il codice IRIDE2 dell'actor" :
	code!=null;

context UseCase ERROR "E'necessario specificare il codice IRIDE2 dello use-case" :
	code!=null;
	
context UCBasedSecurityConstraint ERROR "E' necessario specificare lo use-case a cui il security constraint si riferisce" :
	this.useCase!=null;

context ActorBasedSecurityConstraint ERROR "E' necessario specificare l'actor a cui il security constraint si riferisce" :
	this.actor!=null;
	
context UISecurityConstraint ERROR "Non è possibile specificare un UI security constraint in cui il widget"+
" deve essere abilitato e contemporaneamente invisibile" :
	this.enabled==false || (enabled==true && visible==true);
	