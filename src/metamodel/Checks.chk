import guigen;

extension org::eclipse::xtend::util::stdlib::naming;
extension org::eclipse::xtend::util::stdlib::io;
extension template::GenericExtensions;
extension template::struts2::Struts2Extensions;


///////////////////////////////////
// ultimi progressivi per tipologia
// - GUI: ?
// - GUI-UISTR: 043
// - GUI-BIND: 28
// - GUI-SEC: ?
///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Check indipendenti dalla piattaforma target                                                                      //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

context TargetPlatform WARNING "GUI-001: L'interazione 'ricca' non e' abilitata: se si desidera abilitarla impostare a true "+
	"l'attributo 'enableRichUIBehavior' nell'oggetto TargetPlatform" :
	enableRichUIBehavior == true;

context TargetPlatform WARNING 
	"GUI-002: E' stato impostato l'utilizzo delle feature di interazione 'ricca': tali funzioni sono utilizzabili "+
	" solo con la cartuccia 'neutral' e solo se e' disponibile lo 'skin' per il portale target: "+
	"verificare se le condizioni sussistono; se si desidera disabilitarla impostare a <false> "+
	"l'attributo 'enableRichUIBehavior' nell'oggetto TargetPlatform" :
	enableRichUIBehavior != true;

context TargetPlatform WARNING "GUI-003: L'abilitazione del Fat-Client e' una feature sperimentale: se si desidera disabilitarla impostare a false "+
"l'attributo 'enableFatClient' nell'oggetto TargetPlatform" :
	enableFatClient != true;

context TargetPlatform if enableFatClient == true ERROR "GUI-004: L'abilitazione della modalita' Fat-Client a' possibile solo se sono anche abilitate le feature 'ricche'"+
"nell'oggetto TargetPlatform" :
	enableRichUIBehavior==true;

// validazioni generali (prodotto/componente etc..)
context GUIModel ERROR "GUI-005: E' necessario specificare il codice prodotto (max 10 caratteri)" :
	codProdotto!=null && codProdotto.length<=10;

context GUIModel ERROR "GUI-006: E' necessario specificare il codice componente (max 20 caratteri)" :
	codComponente!=null && codComponente.length<=20;

context GUIModel ERROR "GUI-007: E' necessario specificare la versione del prodotto nel formato major.minor.bugfix" :
	versioneProdotto!=null && isVersioneFormalmenteCorretta(versioneProdotto);

context GUIModel ERROR "GUI-008: E' necessario specificare la versione del componente nel formato major.minor.bugfix" :
	versioneComponente!=null && isVersioneFormalmenteCorretta(versioneComponente);

context GUIModel ERROR "GUI-009: E' necessario scegliere la Target Platform" :
	targetPlatform!=null;

// [AM] warning per incoraggiare l'uso della modularizzazione
context GUIModel WARNING "GUI-010: Per una migliore gestione del progetto di consiglia di definire il SecurityModel in un modello separato, "+
	"includerlo come risorsa e referenziarlo in 'extSecurityModel': "+loc() :
	securityModel==null;

context GUIModel ERROR "GUI-011: E' possibile specificare solo una dei due elementi 'securityModel' (deprecato) e 'extSecurityModel' (consigliato):"+loc() :
	securityModel==null || extSecurityModel==null;	

// [DM] -> I CHECK SULLA HEADER VARIANO A SECONDA DELLA CARTUCCIA
//context Header ERROR "E' necessario impostare i codici 'canale' e 'applicativo' nell'oggetto Header "+
//					 "(servono per referenziare le risorse remote)" :
//	codCanale != null && codCanale != "" && codApplicativo != null && codApplicativo != "";

context Header ERROR "GUI-012: E' necessario impostare i nomi descrittivi del 'canale' e dell' 'applicativo' nell'oggetto Header "+
					 "(servono per il filo d'arianna)" :
	nomeCanale != null && nomeCanale != "" && nomeApplicativo != null && nomeApplicativo != "";
	
context Header ERROR "GUI-013: E'necessario impostare il link (di produzione) del canale nell'oggetto Header (viene inserito nel filo d'arianna)" :
	linkCanale != null && linkCanale.length>0;

// [SC] Check che verifica l'univocita' dei nomi degli appData definiti nel modello	
context GUIModel ERROR "GUI-014: I nomi degli application data devono essere univoci " + loc() :
	applicationDataNamesAreAllUnambiguous(getAllApplicationData(this));	


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Validazione su AppModule context AppModule
context AppModule ERROR "GUI-015: E' necessario specificare il name dell AppModule:"+loc() :
	!isNullOrEmpty(name);
	
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Validazione su pannelli

context ApplicationArea ERROR "GUI-016: Deve essere definito il content panel di homePage: " + loc() :
	this.homePage!=null;

context ApplicationArea WARNING "GUI-017: Per una migliore gestione del progetto si consiglia di modellare gli AppModule in modelli separati, includere il modello e "+
	"referenziarlo nell'ApplicationArea, invece che definire AppModule direttamente nell'ApplicationArea."+
	"AppModules candidati all'esternalizzazione: "+this.modules.name :
	modules.size==0;

context ApplicationArea WARNING "GUI-018: Per una migliore gestione del progetto si consiglia di suddividere il modello in AppModule mantenuti in modelli separati, includere i modelli e "+
	"referenziarlo nell'ApplicationArea, invece che definire ContentPanel direttamente nell'ApplicationArea.":
	contentPanels.size==0;

	
context ContentPanel ERROR "GUI-019: occorre specificare il nome del contentPanel:"+loc() :
	name!=null;

context ContentPanel 
ERROR "GUI-020: Il nome di un content panel deve essere diverso da: home/Home oppure menu/Menu oppure logout/Logout : "+loc():
	!(this.name.toString()=="home") &&  !(this.name.toString()=="Home") &&  
	!(this.name.toString()=="menu") &&  !(this.name.toString()=="Menu") &&
	!(this.name.toString()=="logout") && !(this.name.toString()=="Logout");

context ContentPanel if eRootContainer.metaType == GUIModel 
ERROR "GUI-021: Il nome dei content panel deve essere univoco all'interno dell'applicazione " + loc() :
	isContentPanelUnique(this); 

context ContentPanel ERROR "UISTR-001: E'necessario definire il pannello principale nel content panel:"+loc() :
	panels!=null;
	
context Panel ERROR "GUI-022: E'necessario impostare l'attributo 'name' del pannello " + loc():
	this.name != null && this.name != "";

context FormPanel ERROR "GUI-UISTR-002: E' necessario impostare il layout del pannello:"+loc() :
	layout!=null;

context WidgetsPanel ERROR "GUI-UISTR-003: E' necessario impostare il layout del pannello:"+loc() :
	layout!=null;
	
context CommandPanel ERROR "GUI-UISTR-004: E' necessario impostare il layout del pannello:"+loc() :
	layout!=null;

context MenuPanel ERROR "GUI-UISTR-005: E' necessario impostare il layout del pannello:"+loc() :
	layout!=null;

context Panel if findParentContentPanel(this)!=null 
ERROR "GUI-023: il nome di un pannello deve essere univoco all'interno di un ContentPanel "+loc() :
    findParentContentPanel(this).eAllContents.typeSelect(Panel).select(p|p.name==this.name).size==1;

context Panel if findParentPanelDef(this)!=null 
ERROR "GUI-024: il nome di un pannello deve essere univoco all'interno di un PanelDef "+loc() :
    findParentPanelDef(this).eAllContents.typeSelect(Panel).select(p|p.name==this.name).size==1;

// Con l'introduzione del WidgetsLayout, ha senso che un FormPanel abbia un layout solo se � di primo livello, 
// ovvero se e' figlio diretto di un ContentPanel. In tal caso pu� essere solo:
//   - VerticalFlowPanelLayout => 1 colonna
//   - UDLRCPanelLayout        => 2 o 3 colonne
context FormPanel if this.eContainer.metaType == ContentPanel 
ERROR "GUI-UISTR-006: Il FormPanel di primo livello deve avere un layout di tipo VerticalFlowPanelLayout o UDLRCPanelLayout " + loc() :
	this.layout != null && (this.layout.metaType == VerticalFlowPanelLayout || this.layout.metaType == UDLRCPanelLayout);

// In tutti gli altri casi, anche se il layout e' ininfluente(al momento viene ignorato, visto che il layout dei widgets 
// passa come competenza al WidgetsPanel), imponiamo che sia obbligatorio e sia VerticalFlowPanelLayout
// -> MODIFICATO: VARIA A SECONDA DELLA CARTUCCIA
//context FormPanel if this.eContainer.metaType != ContentPanel ERROR "Il FormPanel di livello inferiore al primo deve avere un layout di tipo VerticalFlowPanelLayout " + loc() :
//	this.layout != null && this.layout.metaType == VerticalFlowPanelLayout;

context FormPanel ERROR "GUI-UISTR-007: E' necessario impostare lo specificatore del layout NSEWC per tutti i sottopannelli "+loc() :
    (this.layout.metaType!=UDLRCPanelLayout) 
    ||
    (
    	(
    		this.layout.metaType==UDLRCPanelLayout 
    		&& 
    		this.subpanels.select(w|w.layoutSpec==null).size==0
    		&&
    		this.subpanels.select(w|w.layoutSpec.metaType!=UDLRCWidgetLayoutSpec).size==0
    	)
    );

//Un UDLRC di primo livello deve avere almeno il pannello CENTER
context FormPanel if (this.layout.metaType==UDLRCPanelLayout && eContainer.metaType==ContentPanel) 
ERROR "GUI-UISTR-008: Un FormPanel di primo livello avente layout UDLRC deve contenere almeno il quadrante CENTER:"+loc() :
	(((List[WidgetLayoutSpecifier])(this.subpanels.layoutSpec)
		.typeSelect(UDLRCWidgetLayoutSpec))
		.select(ls|ls.value==UDLRCSpecConstants::CENTER)).size==1;
		
		
// { STDMDD-605   
context FormPanel if (this.layout.metaType==UDLRCPanelLayout) 
ERROR "GUI-UISTR-009: Un FormPanel avente layout UDLRC non puo' contenere piu' di un pannello nello stesso quadrante (UP):"+loc() :
	(((List[WidgetLayoutSpecifier])(this.subpanels.layoutSpec)
		.typeSelect(UDLRCWidgetLayoutSpec))
		.select(ls|ls.value==UDLRCSpecConstants::UP)).size<=1;

context FormPanel if (this.layout.metaType==UDLRCPanelLayout) 
ERROR "GUI-UISTR-010: Un FormPanel avente layout UDLRC non puo' contenere piu' di un pannello nello stesso quadrante (DOWN):"+loc() :
	(((List[WidgetLayoutSpecifier])(this.subpanels.layoutSpec)
		.typeSelect(UDLRCWidgetLayoutSpec))
		.select(ls|ls.value==UDLRCSpecConstants::DOWN)).size<=1;

context FormPanel if (this.layout.metaType==UDLRCPanelLayout) 
ERROR "GUI-UISTR-011: Un FormPanel avente layout UDLRC non puo' contenere piu' di un pannello nello stesso quadrante (LEFT):"+loc() :
	(((List[WidgetLayoutSpecifier])(this.subpanels.layoutSpec)
		.typeSelect(UDLRCWidgetLayoutSpec))
		.select(ls|ls.value==UDLRCSpecConstants::LEFT)).size<=1;

context FormPanel if (this.layout.metaType==UDLRCPanelLayout) 
ERROR "GUI-UISTR-012: Un FormPanel avente layout UDLRC non puo' contenere piu' di un pannello nello stesso quadrante (RIGHT):"+loc() :
	(((List[WidgetLayoutSpecifier])(this.subpanels.layoutSpec)
		.typeSelect(UDLRCWidgetLayoutSpec))
		.select(ls|ls.value==UDLRCSpecConstants::RIGHT)).size<=1;

context FormPanel if (this.layout.metaType==UDLRCPanelLayout) 
ERROR "GUI-UISTR-013: Un FormPanel avente layout UDLRC non pu� contenere piu' di un pannello nello stesso quadrante (CENTER):"+loc() :
	(((List[WidgetLayoutSpecifier])(this.subpanels.layoutSpec)
		.typeSelect(UDLRCWidgetLayoutSpec))
		.select(ls|ls.value==UDLRCSpecConstants::CENTER)).size<=1;
// STDMDD-605 }
	

	


// Con l'introduzione del WidgetsPanel, il FormPanel non deve pi� contenere widgets.
context FormPanel ERROR "GUI-UISTR-014: Un FormPanel non puo' contenere Widgets. L'attributo widgets e' stato mantenuto solo per retrocompatibilita'. " + loc() :
	this.widgets.size == 0;


context TabSetPanel ERROR "GUI-UISTR-015: Un TabSetPanel deve avere almeno un tab " + loc() :
	this.panels.size>0;


context MultiPanel ERROR "GUI-UISTR-016: Un MultiPanel deve avere almeno un pannello interno " + loc() :
	this.panels.size>0;
	
context MultiPanel ERROR "GUI-UISTR-017: Un multipanel puo' contenere solo FormPanel/CommandPanel/StdMessagePanel come pannelli interni "+loc() :
	this.panels.select(p|(
			!p.metaType.isAssignableFrom(FormPanel)
			&&
			!p.metaType.isAssignableFrom(CommandPanel)
			&&
			!p.metaType.isAssignableFrom(StdMessagePanel)
			&&
			!p.metaType.isAssignableFrom(UserDefinedPanel)
			)
	).size==0;

context MultiPanel ERROR "GUI-DATA-001: Gli item dei multi panel non possono contenere DataWidget dichiarati required: " + loc() :
	findAllWidgetsInPanel(this).select(w|DataWidget.isAssignableFrom(w.metaType) && ((DataWidget)w).required==true).size==0;

context MultiPanel ERROR "GUI-UISTR-018: Un Multi/TabSet/Wizard non puo' essere contenuto in un PanelDef:"+loc() :
	findParentPanelDef() == null;

// [DM] check su CommandPanel
// 1) deve essere contenuto in un FormPanel (es il panel master con layout UDLRC)
// 2) pu� contenere solo CommandWidget (o al massimo HiddenValue)
// 3) pu� avere solo due tipi di layout: [a] horizontal flow, [b] UDLRC ristretto (solo L ed R, tipico delle barre di navigazione)
// 4) non deve avere label (*da verificare*) 
// 1
context CommandPanel ERROR "GUI-UISTR-019: Un CommandPanel deve essere contenuto in un FormPanel, in un DialogPanel o in un MultiPanel/TabSetPanel/WizardPanel " + loc() :
	this.eContainer.metaType == FormPanel ||
	this.eContainer.metaType == DialogPanel ||
	this.eContainer.metaType.isAssignableFrom(MultiPanel);
// 2
context CommandPanel ERROR "GUI-UISTR-020: Un CommandPanel puo' contenere solo CommandWidget o HiddenValue " + loc() :
	commandPanelCommandWidgetCheck(this);
// 3 -> MODIFICATO: VARIA A SECONDA DELLA CARTUCCIA
//context CommandPanel ERROR "Un CommandPanel pu� avere solo due tipi di layout: [a] HorizontalFlowPanelLayout, [b] UDLRCPanelLayout ristretto (solo LEFT ed RIGHT) " + loc() :
//	commandPanelLayoutCheck(this);
// 4
context CommandPanel ERROR "GUI-UISTR-021: Un CommandPanel non deve avere label " + loc() :
	isNullOrEmpty(this.label); // TODO: verificare se � corretto che sia cos�


// [DM] check su MenuPanel
// 1) deve essere contenuto in un FormPanel (es il panel master con layout UDLRC)
// 2) pu� contenere solo widget di tipo MenuView o TreeView
// 4) pu� avere solo un layout Vertical
// 5) non deve avere label (*da verificare*)
// 1
context MenuPanel ERROR "GUI-UISTR-022: Un MenuPanel deve essere contenuto in un FormPanel " + loc() :
	this.eContainer.metaType == FormPanel;
// 2
context MenuPanel ERROR "GUI-UISTR-023: Un MenuPanel puo' contenere solo widget di tipo MenuView o TreeView " + loc() :
	menuPanelWidgetCheck(this);
// 4
context MenuPanel ERROR "GUI-UISTR-024: Un MenuPanel puo' avere solo un layout di tipo VerticalFlowPanelLayout o HorizontalFlowPanelLayout" + loc() :
	this.layout.metaType == VerticalFlowPanelLayout || this.layout.metaType == HorizontalFlowPanelLayout;
// 5
context MenuPanel ERROR "GUI-UISTR-025: Un MenuPanel non deve avere label " + loc() :
	isNullOrEmpty(this.label); // TODO: verificare se � corretto che sia cos�

	
// [DM] check su DialogPanel	
context DialogPanel ERROR "GUI-UISTR-026: Un DialogPanel puo' essere inserito solo in un ContentPanel " + loc() :
	this.eContainer.metaType == ContentPanel;

context DialogPanel ERROR "GUI-UISTR-027: Un DialogPanel deve contenere almeno un MsgBoxPanel " + loc() :
	this.msgBoxes != null && this.msgBoxes.size > 0;


// [DM] check su MsgBoxPanel
context MsgBoxPanel ERROR "GUI-UISTR-028: Un MsgBoxPanel puo' essere inserito solo all'interno di un un DialogPanel o di un FormPanel " + loc() :
	this.eContainer.metaType == DialogPanel || this.eContainer.metaType == FormPanel;

context MsgBoxPanel ERROR "GUI-UISTR-029: Un MsgBoxPanel deve contenere almeno un PlainText " + loc() :
	this.textMessages != null && this.textMessages.size > 0;


// [DM] check su WidgetsPanel
context WidgetsPanel ERROR "GUI-UISTR-030: Un WidgetsPanel deve essere contenuto in un FormPanel " + loc() :
	this.eContainer.metaType == FormPanel || this.eContainer.metaType == PanelDef;

context WidgetsPanel ERROR "GUI-UISTR-031: E' necessario impostare il layout su ogni WidgetsPanel " + loc() :
	this.layout != null;

context WidgetsPanel ERROR "GUI-UISTR-032: Un WidgetsPanel puo' avere i seguenti layout: HorizontalFlow, VerticalFlowPanelLayout o GridPanelLayout " + loc() :
	this.layout.metaType == HorizontalFlowPanelLayout || this.layout.metaType == VerticalFlowPanelLayout || this.layout.metaType == GridPanelLayout;

context WidgetsPanel ERROR "GUI-UISTR-033: Il WidgetsPanel ha il layout 'Grid': e' necessario impostare la posizione in griglia per tutti i widget " + loc() :
	this.layout.metaType != GridPanelLayout 
	||
    (
    	(
    		this.layout.metaType==GridPanelLayout 
    		&& 
    		this.widgets.select(w|w.layoutSpec==null).size==0
    		&&
    		this.widgets.select(w|w.layoutSpec.metaType!=GridWidgetLayoutSpec).size==0
    	)
    );


//context WidgetsPanel if !isNullOrEmpty(this.layout.columnSizes) ERROR "L'attributo columnSizes non � formalmente corretto, il numero di colonne non corrisponde al numero atteso, la somma delle colonne non � 100 " + loc() :
//	columnSizesWidgetsPanelCheck(this);

context WidgetsPanel if !isNullOrEmpty(this.layout.columnSizes) 
ERROR "GUI-UISTR-034: L'attributo columnSizes non e' formalmente corretto: deve contenere solo numeri interi separati da virgola: " + loc() :
	columnSizesFormatWidgetsPanelCheck(this);

context WidgetsPanel if !isNullOrEmpty(this.layout.columnSizes) 
ERROR "GUI-UISTR-035: L'attributo columnSizes non e' formalmente corretto: la somma delle colonne non e' 100: " + loc() :
	columnSizesSumWidgetsPanelCheck(this);

context WidgetsPanel if !isNullOrEmpty(this.layout.columnSizes) 
WARNING "GUI-UISTR-036: L'attributo columnSizes non e' formalmente corretto: il numero di colonne fisiche generate (" +
		realColumnsNumberWidgetsPanelCheck(this) + ") non corrisponde al numero atteso (" +
		expectedColumnsNumberWidgetsPanelCheck(this) + ") : " + loc() :
	realColumnsNumberWidgetsPanelCheck(this) == expectedColumnsNumberWidgetsPanelCheck(this);



//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Validazioni su struttura menu

// [MA] univocita' name Menu/MenuItem
context Menu ERROR "GUI-25: Il nome dei menu deve essere univoco all'interno del modello: "+loc() : 
	isMenuNameUnique(name, ((GUIModel)eRootContainer).structure.appWindow.appArea.menubar);

context Menu if(this.submenu.size == 0) WARNING "GUI-CMD-001: Il menu "+this.name+" non ha nessun EventHandler associato ed � quindi inattivo " + loc() + " Numero di submenus --> " + this.submenu.size :
	this.eventHandler != null ; 

// [DM] STDMDD-241: razionalizzazione e completamento gestione casistiche rendering menu -> MenuItem DEPRECATO
context MenuItem ERROR "GUI-UISTR-037: MenuItem e' DEPRECATO! Utilizzare l'elemento Menu al suo posto: " + loc() : 
	false;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///// Validazione su widget

context Widget ERROR "GUI-026: E'necessario impostare l'attributo 'name' del widget "+loc() :
	this.name != null && this.name != "";

context Widget if findParentContentPanel()!=null 
ERROR "GUI-027: il name dei widgets in un content panel (eccettuati singoli radiobutton) deve essere univoco " + loc() :
     this.metaType==RadioButton ||
     findAllWidgetsInContentPanel(findParentContentPanel(this)).select(w|((Widget)w).name==this.name).size == 1;

context Widget if findParentPanelDef()!=null 
ERROR "GUI-028: il name dei widgets in un panel def (eccettuati singoli radiobutton) deve essere univoco " + loc() :
     this.metaType==RadioButton ||
     findParentPanelDef().eAllContents.typeSelect(Widget).select(p|p.name==this.name).size==1;

     
context Table ERROR "GUI-BIND-001: Non e' stato impostato lo schema delle colonne"+loc() :
	this.columnModel!=null;

context Table if (columnModel.columns.select(c|c.editable).size>0) ERROR "GUI-BIND-002: E' necessario impostare il 'valueSelector' "+
	"se la tabella ha almeno una colonna editabile; serve per passare alla business logic l'id della riga su cui si clicca:"+loc() :
	!isNullOrEmpty(columnModel.valueSelector);

context Table if (databinding != null) ERROR "GUI-BIND-003: E' necessario impostare il 'valueSelector' se la tabella ha un value binding (ovvero se ammette la selezione);"+
	" serve per reperire l'id delle righe selezionate:"+loc() :
	!isNullOrEmpty(columnModel.valueSelector);
	
// [AM] perr ora questa feature � sospesa
context Table ERROR "GUI-UISTR-038: Una table inclusa in un PanelDef non puo' avere un custom decorator:"+loc() :
	findParentPanelDef()==null || this.customDecorator == false;

context ColumnModel ERROR "GUI-BIND-004: Lo schema delle colonne deve contenere almeno una colonna " + loc() :
	columns != null && columns.size>0;
	
// [DM] una colonna non pu� essere contemporaneamente editabile e cliccabile
context Column ERROR "Una colonna non puo' essere contemporaneamente editabile e cliccabile " + loc() :
!(this.editable && this.eventActive);

context Column if !editable ERROR "Non si puo' specificare l'attributo 'editableFlagSelector' se la colonna non e' editabile " + loc() :
	editableFlagSelector == null;

context Column if !eventActive ERROR "Non si puo' specificare l'attributo 'activeFlagSelector' se la colonna non e' attivabile " + loc() :
	activeFlagSelector == null;


context Column if multiDataBinding !=null ERROR "GUI-BIND-005: configurare in alternativa il multidataBinding o il multidataPropertySelector " + loc () :
	multidataPropertySelector == null;
	
context Column if multidataPropertySelector !=null ERROR "GUI-BIND-006: configurare in alternativa il multidataBinding o il multidataPropertySelector " + loc () :
	multiDataBinding == null;

// verifica presenza del campo collegato alla colonna
context Column if !isNullOrEmpty(selector) WARNING "GUI-BIND-007: La colonna "+this.label+" referenzia un campo non risolubile dal selettore '"+this.selector+"'" +loc():
	canResolveColumnBinding(this, selector);

// verifica presenza del campo collegato alla cliccabilit� della colonna
context Column if !isNullOrEmpty(this.activeFlagSelector) WARNING "GUI-BIND-008: La colonna "+this.label+" referenzia un campo non risolubile dal activeFlagSelector '"+this.activeFlagSelector+"'" +loc():
	canResolveColumnBinding(this, activeFlagSelector);

// verifica presenza del campo collegato alla editabilit� della colonna
context Column if !isNullOrEmpty(this.editableFlagSelector) WARNING "GUI-BIND-009: La colonna "+this.label+" referenzia un campo non risolubile dal editableFlagSelector '"+this.editableFlagSelector+"'" +loc():
	canResolveColumnBinding(this, editableFlagSelector);

// il campo maxFieldLength deve essere > 0 solo se la colonna � editabile
context Column if (!editable) WARNING "La colonna "+this.label+" non e' editabile ma ha una dimensione massima impostata: il valore" +
" verr� ignorato; "+loc() :
	fieldMaxLength == 0;

context Column ERROR "Una colonna appartenente ad una tabella inclusa in un PanelDef non puo' avere un custom decorator:"+loc() :
	findParentPanelDef()==null || this.customDecorator == false;
	
/// questi check non vanno bene: da rivedere in seguito gestendo tutte le casistiche
//context Column if !isNullOrEmpty(this.multidataKeySelector) && !isNullOrEmpty(this.multidataPropertySelector) WARNING "La colonna "+this.label+" referenzia un campo non risolubile dal multiDataKeySelector '"+this.multidataKeySelector+"'" +loc():
//	let tableElType = getTableElementsType(getOwnerTable(this)):
//	let mdPropField = tableElType != null ? getSelectedField(null,tableElType, multidataPropertySelector) : ((Field)null) :
//	if (mdPropField != null && ((Field)mdPropField).type!=null) then
//		getSelectedField(null, ((Field)mdPropField).type, this.multidataKeySelector)!=null
//	else
//		true;

//context Column if !isNullOrEmpty(this.multidataPropertySelector) WARNING "La colonna "+this.label+" referenzia un campo non risolubile dal multiDataPropertySelector '"+this.multidataPropertySelector+"'" +loc():
//	canResolveColumnBinding(this, multidataPropertySelector);

//context Column if !isNullOrEmpty(this.multidataValueSelector) WARNING "La colonna "+this.label+" referenzia un campo non risolubile dal multiDataValueSelector '"+this.multidataValueSelector+"'" +loc():
//	canResolveColumnBinding(this, multidataValueSelector);


	 	
// [DM] HiddenValue puo' essere inserito solo in un CommandPanel (per il momento...): questo per non complicare le cose con i layout
context HiddenValue ERROR "GUI-UISTR-039: Il widget HiddenValue puo' essere inserito solo in un Pannello con Vertical o Horizontal layout " + loc() :
 	((Panel)this.eContainer).layout.metaType == VerticalFlowPanelLayout || ((Panel)this.eContainer).layout.metaType == HorizontalFlowPanelLayout;
 


	
context Button if (metaType != ResetButton) ERROR "GUI-CMD-002: Ogni pulsante non di reset deve avere un eventHandler associato "+ loc() :
	this.eventHandlers.select(e|e.eventType == EventTypes::CLICKED).size > 0;  
 
// [DM] Calendar
context Calendar ERROR "GUI-BIND-010: Un Calendar deve avere un DataType di tipo DATE " + loc() :
 	this.dataType.metaType == SimpleType && ((SimpleType)(this.dataType)).code == guigen::SimpleTypeCodes::DATE;

context Calendar if (this.databinding != null) ERROR "GUI-BIND-011: Il DataBinding di un Calendar deve essere di tipo Date " + loc() :
	calendarDataBindingTypeCheck(this);


context TabSwitcher ERROR "GUI-UISTR-040: Il Tab Switcher puo' essere associato solo ad un TabSetPanel o a un WizardPanel:" +loc() :
  	this.eContainer.metaType == TabSetPanel || this.eContainer.metaType == WizardPanel;

context MapView ERROR "GUI-029: E' necessario impostare l'id della mappa da visualizzare:"+loc() :
	!isNullOrEmpty(mapId);
	
context MapView ERROR "GUI-CMD-003: Ad una mappa si puo' associare un solo evento di tipo CLICKED e/o un evento di tipo EDIT_COMMITTED:"+loc() :
	this.eventHandlers.size == 0 || 
	this.eventHandlers.select(evh|evh.eventType != EventTypes::CLICKED && evh.eventType != EventTypes::EDIT_COMMITTED).size==0;

context MapView ERROR "GUI-BIND-012: E' necessario specificare il value-binding per la mappa:"+loc() :
	this.databinding != null;	
	
context RadioButtons if (this.layout!=null) ERROR "GUI-UISTR-041: I radiobutton possono essere orientati esclusivamente in verticale e orizzontale" + loc() :
	this.layout.metaType == VerticalFlowPanelLayout || this.layout.metaType == HorizontalFlowPanelLayout;
	 	
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// Validazione su azioni e listener

context MenuItem ERROR "GUI-CMD-004: Un menu item puo' generare solo eventi di tipo CLICKED "+loc() :
	this.eventHandler==null || this.eventHandler.eventType == EventTypes::CLICKED;

context EventHandler ERROR "GUI-CMD-036: Un action Listener deve innescare un comando a fronte di un tipo di evento "+loc() :
	this.command!=null;

// nel caso del KEY_PRESSED su TextField => suggestion il comando associato deve essere di tipo simpleExec
context EventHandler if (eContainer.metaType == TextField && eventType == EventTypes::KEY_PRESSED) 
	ERROR "GUI-CMD-005: Ad un KEY_PRESSED di un TextField deve essere associato solo un ExecCommand ad esito univoco (unico Outcome con esecuzione di NOPCommand):"+loc() :
	isSimpleExecChain(this.command);
	
context JumpCommand ERROR "GUI-CMD-006: E'necessario impostare il pannello target di un Jump Command "+loc() :
	this.jumpTo != null;
	
context JumpCommand WARNING "GUI-CMD-007: L'utilizzo di JumpCommand sullo stesso pannello di partenza e' deprecato. Utilizzare se possibile un NOPCommand: "+loc() :
	!jumpToSameContentPanel(this);

context ShowDialogCommand ERROR "GUI-CMD-008: E'necessario impostare il dialog target di uno ShowDialogCommand "+loc() :
    this.dialog != null;
    
context ExecCommand ERROR "GUI-CMD-009: Un ExecCommand deve avere almeno un CommandOutcome associato "+loc() :
	this.results!=null && this.results.size>0;

context ExecCommand ERROR "GUI-030: E'necessario specificare il nome del metodo associato all'esecuzione "+loc() :
	this.methodName!=null && this.methodName.length>0;

context ActivateMultiPanelItemCommand ERROR "GUI-CMD-010: E'necessario specificare il multi panel "+loc() :
	this.multipanel!=null;
		
context ActivateMultiPanelItemCommand ERROR "GUI-CMD-011: Il sottopannello (activeItem) non e' un item del MultiPanel specificato "+loc() :
	this.activeItem==null || 
	this.multipanel.panels.select(p|p==this.activeItem).size>0;

context JumpExtCommand ERROR "GUI-CMD-012: E' necessario specificare un url statico o un application data a cui puntare a runtime "+loc() :
	this.staticUrl!=null && this.runtimeUrlProvider==null 
	||
	(this.staticUrl==null || this.staticUrl.length==0) && this.runtimeUrlProvider!=null;

context JumpExtCommand ERROR "GUI-031: E'necessario fornire un codice identificativo/descrittivo dell'url esterno a cui saltare "+loc() :
	this.locationCode!=null && this.locationCode.length>0;
	
context JumpExtCommand ERROR "GUI-CMD-013: JumpExtCommand puo' utilizzare solo un appData di tipo string come runtime url provider "+loc() :
	this.runtimeUrlProvider==null ||
	this.runtimeUrlProvider.type.metaType==SimpleType &&
	((SimpleType)(this.runtimeUrlProvider.type)).code==guigen::SimpleTypeCodes::STRING;

context VisibilityCommand ERROR "GUI-CMD-014: Il comando di commutazione di visibilita' e' incomaptibile con i widget dichiarati required "+loc() :
	this.targetWidgets.select(w|DataWidget.isAssignableFrom(w.metaType) && ((DataWidget)w).required==true).size==0;


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// constraints su azioni

// un menu view non puo' avere event handler associati (eredita quelli del menu vero e proprio di cui
// � segnaposto
context MenuView ERROR "GUI-CMD-015: ad una MenuView non e' possibile associare evnet handler, in quanto eredita gli event handler"+
						" della struttura menu globale (di cui funge solo da segnaposto) "+loc() :
	this.eventHandlers.size == 0;

// un Treeview pu� avere solo un event handler di tipo click o node_expanded
context TreeView ERROR "GUI-CMD-016: ad una TreeView e' possibile associare solo la gestione di un evento CLICKED o NODE_EXPANDED"+loc() :
	this.eventHandlers.select(eh|eh.eventType!=EventTypes::CLICKED && eh.eventType!=EventTypes::NODE_EXPANDED).size==0;

// per rendere "eventActive" una colonna di una table occorre specificare un event handler clicked nella table
context Column ERROR "GUI-CMD-017: una colonna di una Table puo' essere dichiarata 'eventActive' solo se la tabella "+
	"definisce un Event Handler di tipo CLICKED: "+loc() :
	!eventActive ||
	(eventActive)
	&& 
	((Table)(((ColumnModel)this.eContainer).eContainer))
		.eventHandlers.select(eh|eh.eventType==EventTypes::CLICKED).size == 1;


context ContentPanel WARNING 
	"GUI-CMD-018: l'utilizzo di azioni on-refresh legacy mode (comandi definiti come child del content panel)"+
	" e' fortemente sconsigliato. Si consiglia di sostituire con il corrispondente CPCommand(ON_REFRESH) ristrutturando "+
	" eventualmente la logica per sfruttare le varie tipologie di CPCommand (ON_ENTER, ON_INIT, ON_REFRESH, BEFORE/AFTER_EVENT):"+loc() :
	this.onRefreshCommand == null;
	
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//// constraints su binding
context ContentPanel if (panels!=null) ERROR "GUI-032: Il content panel "+name+" non referenzia tutti gli Application Data "+
	"referenziati dai widget in esso contenuti. AppData mancanti: "+findUnresolvedAppDataBinding() :
	findUnresolvedAppDataBinding().size == 0; 
	
context AppDataBinding ERROR "GUI-BIND-013: E'necessario specificare il dato associato ad un DataBinding "+loc() :
	this.appData != null;

//STDMDD-918	
context ApplicationData if(this.type!=null && this.type.metaType==SimpleType) 
ERROR "GUI-BIND-014: Non e' possibile effettuare un binding ad un tipo primitivo, utilizzare il corrispondente tipo wrapped." +loc() : 
	let st=(SimpleType)this.type	:  
	!(st.code == SimpleTypeCodes::INT && st.nillable==false) && !(st.code == SimpleTypeCodes::LONG && st.nillable==false)
	&& !(st.code == SimpleTypeCodes::FLOAT && st.nillable==false) && !(st.code == SimpleTypeCodes::DOUBLE && st.nillable==false);

context ApplicationData ERROR "GUI-033: E' necessario specificare il nome dell Application Data "+loc() :
	this.name != null && this.name.length > 0;

context ApplicationData ERROR "GUI-DATA-002: E' necessario specificare il tipo dell Application Data "+loc() :
	this.type != null;
 
context TreeView WARNING "GUI-BIND-015: La fonte dati di un TreeView puo' solo essere un application data di tipo TreeNode e invece e' "+this.multiDataBinding.appData.type+", "+loc() :
	this.multiDataBinding == null ||
	(
		this.multiDataBinding.appData != null 
	 	&& 
	 	this.multiDataBinding.appData.type.metaType == SimpleType 
	 	&&
	 	((SimpleType)this.multiDataBinding.appData.type).code == SimpleTypeCodes::TREE_NODE
	 );


context TreeView if (multiDataBinding != null) ERROR "GUI-BIND-016: La fonte dati di un TreeView non puo' essere un ApplicationData a scope ACTION:"+loc() :
	this.multiDataBinding.appData.lifetimeExtent != DataLifetimeType::USER_ACTION;


//context MultiDataWidget ERROR "Il tipo del binding di un MultiDataWidget deve essere un array di tipo complesso, invece � "+this.multiDataBinding.appData.type+", "+loc() :
//	this.multiDataBinding.appData.type.metaType==TypedArray && 
//	((TypedArray)multiDataBinding.appData.type).componentType.metaType==ComplexType;	

context TextField if multiDataBinding != null ERROR "GUI-BIND-017: Il collection-binding associato ad un TextField deve essere di tipo Array[SimpleType] (suggestion solo testuale)" :
	multiDataBinding.appData.type.metaType == TypedArray &&
	((TypedArray)(multiDataBinding.appData.type)).componentType.metaType == SimpleType;
	
context Field if !name.isNullOrEmpty()  ERROR "GUI-034: In un complexType il nome dei fields deve essere univoco" : 
	((ComplexType) eContainer).getAllFields().select(e|e.name == name).size == 1;
	
context DataWidget ERROR "GUI-BIND-018: E' necessario specificare il tipo del Widget "+loc() :
	this.dataType != null;


context DataWidget if (this.databinding != null && this.metaType != Table) 
WARNING "GUI-BIND-019: Il tipo del widget non corrisponde con il tipo del databinding "+loc() : 
	widgetDataBindingCorrispondenceCheck(this);

// il TextField necessita del binding solo se ha un KEY_PRESSED attivo => suggestion
// la MapView necessita di collection binding solo in caso di acetate layer (geometrie)
// lo slider ha il collection binding opzionale (in altro controllo verifica se static bounds)
context MultiDataWidget if (metaType != TextField && metaType != MapView && metaType != Slider) 
ERROR "GUI-BIND-020: E' necessario impostare il collection-binding "+loc() :
	multiDataBinding != null;

context TextField if (this.eventHandlers.select(eh|eh.eventType == EventTypes::KEY_PRESSED).size > 0) 
	ERROR "GUI-BIND-021: Un TextField con un handler di evento KEY_PRESSED deve avere un collection-binding:"+loc() :
	this.multiDataBinding != null;

context TextField if(this.multiDataBinding != null) ERROR "GUI-BIND-022: Il collection-binding di un TextField deve essere impostato "+
	"solo in presenza di un handler di evento KEY_PRESSED (per realizzare la funzione di suggestion):"+loc() :
 	this.eventHandlers.select(eh|eh.eventType == EventTypes::KEY_PRESSED).size > 0;
	
context Table ERROR "GUI-BIND-023: Non e' stato specificato il binding alla collection per la table "+name :
	this.multiDataBinding != null;
	
context TypeNamespace ERROR "GUI-035: E'necessario valorizzare il nome del type namespace:"+loc() :
	!isNullOrEmpty(this.name); 

context Type ERROR "GUI-036: E'necessario valorizzare il nome di un tipo:"+loc() :
	!isNullOrEmpty(this.name);

context TypedArray ERROR "GUI-DATA-003: E'necessario valorizzare il tipo degli elementi contenuti in un TypedArray:"+loc() :
	componentType != null;

context TypedArray if (componentType.metaType == TypedArray) ERROR "GUI-DATA-004: Il typed Array \""+name+"\" ha se stesso come component type: definizione ricorsiva proibita."+loc() :
	componentType != this;
	
context TypedArray if (componentType.metaType == TypedArray) WARNING "GUI-DATA-005: Il typed Array \""+name+"\" ha un altro TypedArray come component type: il funzionamento non � garantito (caso non previsto)."+loc() :
	false; // sempre errore	

context ComplexType ERROR "GUI-DATA-006: Il ComplexType \""+name+"\" ha una gerarchia con dipendenza circolare:"+loc() :
	!hasCircularHierarchy(this);

// STDMDD-637: verifica conflitto di nome in type namespace
context Type if (eContainer.metaType == TypeNamespace) && !isNullOrEmpty(name) ERROR "GUI-037: Esiste piu' di un Type con lo stesso nome ("+name+") nello stesso TypeNamespace ("+((TypeNamespace)eContainer).name+")" :
	let tns = (TypeNamespace)eContainer :
	tns.types.select(t|t.name == this.name).size==1;
	
//[DM] 
context SimpleType if (this.userDefined) ERROR "GUI-DATA-007: Un SimpleType User Defined con codice INT, FLOAT, LONG, DOUBLE, BOOLEAN, BYTE deve essere nillable: "+loc() :
	if (
		(this.code == SimpleTypeCodes::INT) ||
		(this.code == SimpleTypeCodes::FLOAT) ||
		(this.code == SimpleTypeCodes::LONG) ||
		(this.code == SimpleTypeCodes::DOUBLE) ||
		(this.code == SimpleTypeCodes::BOOLEAN) ||
		(this.code == SimpleTypeCodes::BYTE)
	) 
	then
		this.nillable
	else
		true;	
		
context SimpleType if (this.userDefined) ERROR "GUI-DATA-008: Un SimpleType User Defined con codice STRING, DATE, DATETIME, HOURS, TREE_NODE non deve essere nillable: "+loc() :
	if (
		(code == SimpleTypeCodes::STRING) ||
		(code == SimpleTypeCodes::DATE) ||
		(code == SimpleTypeCodes::DATETIME) ||
		(code == SimpleTypeCodes::HOURS) ||
		(code == SimpleTypeCodes::TREE_NODE)
	)
	then
		!this.nillable
	else
		true;

//////////////////////////////////////////////////////////////////////////////
//// constraints su PanelDef
//////////////////////////////////////////////////////////////////////////////
context AppDataMappingPDefVal ERROR "GUI-038: il parametro indicato per la valorizzazione deve essere un AppDataMappingParam:"+loc() :
	this.param.metaType == AppDataMappingParam;

context TypeDefMappingPDefVal ERROR "GUI-039: il parametro indicato per la valorizzazione deve essere un TypeDefMappingParam:"+loc() :
	this.param.metaType == TypeDefMappingParam;

context ActorMappingPDefVal ERROR "GUI-040: il parametro indicato per la valorizzazione deve essere un ActorMappingParam:"+loc() :
	this.param.metaType == ActorMappingParam;

context RoleMappingPDefVal ERROR "GUI-041: il parametro indicato per la valorizzazione deve essere un RoleMappingParam:"+loc() :
	this.param.metaType == RoleMappingParam;
	
context UCMappingPDefVal ERROR "GUI-042: il parametro indicato per la valorizzazione deve essere un UseCaseMappingParam:"+loc() :
	this.param.metaType == UseCaseMappingParam;

context PDefParamVal ERROR "GUI-043: occorre specificare il parametro valorizzato:"+loc() :
	this.param != null;
	
context AppDataMappingPDefVal ERROR "GUI-044: occorre specificare l'application data target:"+loc() :
	actualAppData!=null;
	
context TypeDefMappingPDefVal ERROR "GUI-045: occorre specificare il tipo target:"+loc() :
	actualTypeDef!=null;
	
context ActorMappingPDefVal ERROR "GUI-046: occorre specificare l' 'Actor' target:"+loc() :
	actualActor!=null;			
	
context RoleMappingPDefVal ERROR "GUI-047: occorre specificare il 'Role' target:"+loc() :
	actualRole!=null;
	
context UCMappingPDefVal ERROR "GUI-048: occorre specificare lo 'UseCase' target:"+loc() :
	actualUseCase!=null;

context PanelDefUse if this.panelDef!=null && this.panelDef.params.size>0 ERROR 
	"GUI-049: occorre valorizzare tutti i "+this.panelDef.params.size+" parametri del PanelDef:"+loc() :
	this.config != null && this.config.paramValues.size == this.panelDef.params.size;	

context PanelDef ERROR "GUI-UISTR-042: Non e' possibile utilizzare un PanelDefUse all'interno di un PanelDef:"+loc() :
	this.eAllContents.typeSelect(PanelDefUse).size==0;

// [AM] rimuovere il check quando sar� implementata la funzione
context PanelDef ERROR "GUI-050: onRefreshCommand su PanelDef: feature non implementata:"+loc() :
	this.onRefreshCommand == null;

//////////////////////////////////////////////////////////////////////////////
//// constraints su commands
//////////////////////////////////////////////////////////////////////////////	
context CommandOutcome ERROR "GUI-CMD-018: Un CommandOutcome deve avere associata un comando effettivo "+loc() :  
	this.command != null;
	
context SequenceCommand ERROR "GUI-CMD-019: Una SequenceCommand deve prevedere almeno uno step nella sequenza di comandi "+loc() :
	this.commands!= null && this.commands.size>0;

context SequenceCommand ERROR "GUI-CMD-020: Solo l'ultimo step di un SequenceCommand pu� essere un JumpCommand "+loc() :
	(this.commands.select(a|a.metaType == JumpCommand).size==0) ||
	  (this.commands.select(a|a.metaType == JumpCommand).size==1) && (this.commands.last().metaType == JumpCommand)
	;

context SequenceCommand ERROR "GUI-CMD-021: Solo l'ultimo step di un SequenceCommand pu� essere un JumpExtCommand "+loc() :
	(this.commands.select(a|a.metaType == JumpExtCommand).size==0) ||
	  (this.commands.select(a|a.metaType == JumpExtCommand).size==1) && (this.commands.last().metaType == JumpExtCommand)
	;

context SequenceCommand ERROR "GUI-CMD-022: Solo l'ultimo step di un SequenceCommand pu� essere un JumpBackCommand "+loc() :
	(this.commands.select(a|a.metaType == JumpBackCommand).size==0) ||
	  (this.commands.select(a|a.metaType == JumpBackCommand).size==1) && (this.commands.last().metaType == JumpBackCommand);

context CommandOnWidgets ERROR "GUI-CMD-023: I comandi che hanno come oggeto un widget devono essere associati ad almeno un widget o ad un elemento di menu"+loc() :
	 (this.targetMenuElement.size>0 || targetWidgets.size>0);

context RefreshViewCommand WARNING "GUI-CMD-024: I pannelli indicati come target di refresh non sono disgiunti; "+
	"elenco dei pannelli che hanno almeno un contenitore gia' referenziato:"+getRefreshViewConflicts(this)+","+loc() :
	let conflicts = getRefreshViewConflicts(this):
	conflicts == null || conflicts.size == 0;

context ContentPanel if (states!=null && states.states.size >0) 
WARNING "GUI-CMD-025: I widget "+getNeverShownWidgets().name
		+" non sono mai visibili in nessuno ScreenState."+loc() :
	let nsw = getNeverShownWidgets() :
	nsw==null || nsw.size==0;

context ContentPanel if (states!=null && states.states.size >0) 
WARNING "GUI-CMD-026: I widget "+getNeverEnabledWidgets().name
		+" non sono mai abilitati in nessuno ScreenState."+loc() :
	let nsw = getNeverEnabledWidgets() :
	nsw==null || nsw.size==0;

context ContentPanel if (this.onRefreshCommand != null) 
ERROR "GUI-CMD-027: La catena di comandi di 'onRefresh' del ContentPanel "+name+" non pu� terminare con JumpCommand o JumpExtCommand o RefreshViewCommand" :
	let leafs = getLeafCommands(this.onRefreshCommand) :
	leafs.select(c|c.metaType == JumpCommand || c.metaType == JumpExtCommand || c.metaType == RefreshViewCommand).size==0;
 	
// le catene di comandi associate all' onAppInit devoono avere come foglie o un JumpCommand o un JumpExtCommand
context ApplicationArea if (this.onInitCommand!=null) 
ERROR "GUI-CMD-028: la logica di inizializzazione dell'applicazione "+
	" (ApplicationArea.onInitCommand) deve avere come esito ultimo solo comandi di Jump o JumpExt, invece contiene anche:"
		+getLeafCommands().select(c|c.metaType != JumpCommand && c.metaType != JumpExtCommand)+"; "+loc():
	let leafs = getLeafCommands() :
	leafs == null || leafs.select(c|c.metaType != JumpCommand && c.metaType != JumpExtCommand).size==0;

////////////////////////////////////////////////////////////////////////////////////////
/// constraints su security model
////////////////////////////////////////////////////////////////////////////////////////

context SecurityModel if(this.autenticationMethod.metaType != CustomAuthentication ) 
ERROR "GUI-SEC-001: Occorre specificare il codice identificativo dell'applicazione per il sistema di sicurezza (es. id app. per IRIDE2) "+loc() :
	this.securityAppID!=null;

context OPAUTHSSO ERROR "GUI-SEC-002: I liveli minimi di autenticazione possibili sono: [1]=username+passwd, [2]=username+passwd+pin, [3]=certificato "+loc() :
	this.minAuthLevel>=1 && this.minAuthLevel<=3;

context CustomSecurityConstraint ERROR "GUI-SEC-003: E' necessario specificare il nome del metodo: "+loc() :
	!isNullOrEmpty(this.methodNameSuffix);

context UISecurityConstraint if (this.eRootContainer.metaType==GUIModel  && this.metaType != DeclarativeUIConstraint && this.metaType != CustomSecurityConstraint) 
ERROR "GUI-SEC-003: Non e' possibile specificare constraint di AUTORIZZAZIONE "+
"se non e' specificato un metodo di AUTENTICAZIONE "+loc() :
	((GUIModel)this.eRootContainer).securityModel.autenticationMethod != null ||
	((GUIModel)this.eRootContainer).extSecurityModel.autenticationMethod != null;

context UISecurityConstraint if (this.eRootContainer.metaType==AppModule && this.metaType != DeclarativeUIConstraint && this.metaType != CustomSecurityConstraint) ERROR "GUI-SEC-004: Non e' possibile specificare constraint di AUTORIZZAZIONE "+
"se non e' specificato un metodo di AUTENTICAZIONE. "+
"E' necessario referenziare nell' AppModule "+ ((AppModule)this.eRootContainer).name + " l' extSecurityModel "
+loc() :
	((AppModule)this.eRootContainer).extSecurityModel.autenticationMethod != null;

context Widget ERROR "GUI-UISTR-044: E' possibile specificare solo un DeclarativeUIConstraint per ciascun Widget e per ciascun comportamento: "+loc() :
	this.securityConstraints.typeSelect(DeclarativeUIConstraint).select(c|c.enabled && !c.visible).size <=1 &&
	this.securityConstraints.typeSelect(DeclarativeUIConstraint).select(c|!c.enabled && c.visible).size <=1;
	
context DeclarativeUIConstraint ERROR "GUI-UISTR-045: E' necessario specificare lo script del constraint: "+loc() :
	!this.script.isNullOrEmpty();
	
context Actor ERROR "GUI-SEC-005: E'necessario specificare il codice IRIDE2 dell'actor "+loc() :
	code!=null;

context Role ERROR "GUI-SEC-006: E'necessario specificare il codice IRIDE2 del role "+loc() :
	code!=null;

context UseCase ERROR "GUI-SEC-007: E'necessario specificare il codice IRIDE2 dello use-case "+loc() :
	code!=null;
	
context UCBasedSecurityConstraint ERROR "GUI-SEC-008: E' necessario specificare lo use-case a cui il security constraint si riferisce "+loc() :
	this.useCase!=null;

context ActorBasedSecurityConstraint ERROR "GUI-SEC-009: E' necessario specificare l'actor a cui il security constraint si riferisce "+loc() :
	this.actor!=null;

context RoleBasedSecurityConstraint ERROR "GUI-SEC-010: E' necessario specificare il role a cui il security constraint si riferisce "+loc() :
	this.role!=null;

	
context UISecurityConstraint ERROR "GUI-SEC-011: Non e' possibile specificare un UI security constraint in cui il widget"+
" deve essere abilitato e contemporaneamente invisibile "+loc() :
	this.enabled==false || !(enabled==true && visible==true);

context UISecurityConstraint ERROR "GUI-SEC-012: Non e' possibile specificare un UI security constraint su un widget dichiarato required: "+loc() :
	(DataWidget.isAssignableFrom(eContainer.metaType)?
		(((DataWidget)eContainer).required==false) 
		:
	 	true
	);

/// verifica se sono presenti il tipo UserInfo e l'app data currentUser
context GUIModel if (securityModel != null || extSecurityModel != null) 
ERROR "GUI-DATA-009: Se e' presente l'autenticazione deve essere definito il tipo 'UserInfo'" :
	((List[Type])getAllTypes(this)).findByName("UserInfo") != null;	
	
context GUIModel if (securityModel != null || extSecurityModel != null) 
ERROR "GUI-DATA-010: Se e' presente l'autenticazione deve essere definito l'application data 'currentUser'" :
	((List[ApplicationData])getAllApplicationData(this)).findByName("currentUser") != null;


// check di referenziazione dell'Appdata currentUser dai pannelli interni al GUIModel
context ContentPanel if (eRootContainer.metaType==GUIModel) 
ERROR "GUI-BIND-024: Se e' presente l'autenticazione ogni content panel deve referenziare l'application data \"currentUser\"" :
	((GUIModel)this.eRootContainer).securityModel==null ||
	appData.select(ab|ab.name=="currentUser").size==1;

// check di referenziazione dell'appdata current user dai pannelli inclusi (usa feature avanzate di extend!)
context ApplicationArea if (extModules.size>0 && ((GUIModel)eRootContainer).securityModel!=null) ERROR 
"GUI-BIND-025: Se e' presente l'autenticazione ogni content panel deve referenziare l'application data \"currentUser\""+
"- pannelli che non referenziano:"+extModules.contentPanels.select(cp|cp.appData.select(ab|ab.name=="currentUser").size==0).name :
	extModules.contentPanels.appData.select(ab|ab.name=="currentUser").size==extModules.contentPanels.size;

// verifiche su datatype modifier pe revitare sql-injection o simili
context DataWidget 
if (dataType.metaType == SimpleType 
	&& ((SimpleType)dataType).userDefined!=true 
	&& dataType.isJavaTypePossiblyFreeText())
WARNING "GUI-BIND-027: "+checkBindingValidation(this)+
	" Un campo di input che corrisponde ad una proprieta' java di tipo stringa se non controllato puo' essere soggetto ad attacchi di tipo SQL-Injection, si consiglia pertanto di modellare"+
	" un dataTypeModifier o di verificare il valore immesso nella business logic che lo utilizza. "+loc() :
let bindingValidationCheckResult = checkBindingValidation(this) :
	bindingValidationCheckResult  == "OK";


context DataWidget
if (databinding!=null && databinding.appData != null && databinding.appData.type.metaType == SimpleType)
ERROR "GUI-BIND-028: non e' possibile modellare un constraint di validazione su un widget che ha un binding verso un application data di tipo semplice. Eliminare il constraint dal modello e gestire la validazione da business logic."+loc():
dataTypeModifier.isNullOrEmpty();

context ApplicationArea ERROR "GUI-UISTR-043: E' necessario specificare una MenuBar (anche vuota) nell'ApplicationArea"+loc() :
	menubar != null;	
	
///// ActivationModel

context ActivationParam ERROR "GUI-051: E'necessario specificare il nome del paramtero di attivazione:"+loc() :
	!isNullOrEmpty(name);

context ActivationParam ERROR "GUI-BIND-026: E'necessario specificare il tipo del parametro di attivazione:"+loc() :
	type != null;
	
/// check per evitare che i parametri vadano in conflitto con app data o property implicite dei widget
context ActivationParam if (!isNullOrEmpty(name))ERROR "GUI-052: Un paramtero di attivazione non puo avere un nome che inizia con 'AppData/appData' o 'widg_/Widg_':"+loc() :
	!name.toLowerCase().startsWith("appdata") && !name.toLowerCase().startsWith("widg_");
	
///// Custom cartridges
context CustomCartridge WARNING "GUI-053: La cartuccia non definisce nessun template" :
	templates.size>0;

context CustomCartridge ERROR "GUI-054: E'necessario specificare il nome della cartuccia:" + loc() :
	!isNullOrEmpty(name);

context CustomCartridge ERROR "GUI-055: E' necessario specificare l'ID della cartuccia:" + loc() :
	crtID != null;

context CustomTemplate ERROR "GUI-056: E'necessario impostare l'ID del template:"+loc() :
	!isNullOrEmpty(tplID); 

context CustomTemplate ERROR "GUI-057: E'necessario impostare il nome del template:"+loc() :
	!isNullOrEmpty(name);
	
context  ContentPanel if(this.cpCommands!=null) 
ERROR "GUI-CMD-029: Un ContentPanel non puo' contenere più di un CpCommands di tipo ON_REFRESH " : 
this.cpCommands.commands.select(c|c.execute==CPCommandExecutionTypes::ON_REFRESH).size <= 1;

context ContentPanel 
ERROR "GUI-CMD-030: Un ContentPanel non puo' contenere un CpCommands di tipo ON_REFRESH e un onRefreshCommand contemporaneamente " :
!(this.onRefreshCommand!=null && this.cpCommands.commands.select(c|c.execute==CPCommandExecutionTypes::ON_REFRESH).size == 1);

context  ContentPanel if(this.cpCommands!=null) 
ERROR "GUI-CMD-031: Un ContentPanel non puo' contenere più di un CpCommands di tipo ON_INIT " : 
this.cpCommands.commands.select(c|c.execute==CPCommandExecutionTypes::ON_INIT).size <= 1;

context  ContentPanel if(this.cpCommands!=null) 
ERROR "GUI-CMD-032: Un ContentPanel non puo' contenere più di un CpCommands di tipo ON_ENTER " : 
this.cpCommands.commands.select(c|c.execute==CPCommandExecutionTypes::ON_ENTER).size <= 1;

context  ContentPanel if(this.cpCommands!=null) 
ERROR "GUI-CMD-033: Un ContentPanel non puo' contenere più di un CpCommands di tipo BEFORE_EVENTS " : 
this.cpCommands.commands.select(c|c.execute==CPCommandExecutionTypes::BEFORE_EVENTS).size <= 1;

context  ContentPanel if(this.cpCommands!=null) 
ERROR "GUI-CMD-034: Un ContentPanel non puo' contenere più di un CpCommands di tipo AFTER_EVENTS " : 
this.cpCommands.commands.select(c|c.execute==CPCommandExecutionTypes::AFTER_EVENTS).size <= 1;

context CPCommand if (this.command != null ) 
ERROR "GUI-CMD-035: La catena di comandi di "+ this.execute+ " del ContentPanel "+ ((ContentPanel)this.eContainer.eContainer).name + " non pu� terminare con JumpCommand o JumpExtCommand o RefreshViewCommand" :
	let leafs = getLeafCommands(this.command) :
	leafs.select(c|c.metaType == JumpCommand || c.metaType == JumpExtCommand || c.metaType == RefreshViewCommand).size==0;	
	
// Secuity Profile	
context SecurityProfile ERROR "GUI-SEC-013: E' necessario impostare IdentityAdapter nel SecurityProfile":
	this.idAdapter!=null;	
	
context SecurityProfile ERROR "GUI-SEC-014: Un SecurityProfile deve contenere almeno un tipo di AuthenticationGuard":
	this.intAuthGuard!=null || this.extAuthGuard!=null;
	
context SecurityProfile ERROR "GUI-SEC-015: E' necessario impostare PEPImplementation nel SecurityProfile":
	this.pepImpl!=null;	
	
context SecurityProfile if (this.intAuthGuard!=null)  ERROR "GUI-SEC-016: Un SecurityProfile deve contenere un solo tipo di AuthenticationGuard":
	this.extAuthGuard==null;	
	
context IdentityAdapter ERROR "GUI-SEC-017: E' necessario impostare infoSourceName nel IdentityAdapter"+loc():
	this.infoSourceName !=null;		

context InternalAuthenticationGuard ERROR "GUI-SEC-018: E' necessario impostare il tipo LocalModule in InternalAuthenticationGuard"  :
this.loginModule!=null;

context LoginModule if(this.internal == true) ERROR "GUI-SEC-019: Una InternalAuthenticationGuard con LoginModule interna deve contenere almeno un loginUI" :
this.loginUI.size > 0;
context LoginModule if(this.internal == true) ERROR "GUI-SEC-020: Una InternalAuthenticationGuard con LoginModule interna non deve contenere piu' di un loginUI" :
this.loginUI.size <= 1;
context TargetPlatform if (this.securityProfile!=null) ERROR "GUI-SEC-021: SecurityProfile prevede un CustomAuthentication" : 
((GUIModel)this.eRootContainer).extSecurityModel!=null && ((GUIModel)this.eRootContainer).extSecurityModel.autenticationMethod.metaType == CustomAuthentication ;

context InlineCodeSnippet if this!=null ERROR "GUI-058: E' necessario valorizzare la property lang"+loc() :
	!this.lang.isNullOrEmpty();
context InlineCodeSnippet if this!=null ERROR "GUI-059: E' necessario valorizzare la property snippetCode"+loc() :
	!this.snippetCode.isNullOrEmpty();
context InlineCodeSnippet if this!=null ERROR "GUI-060: E' necessario valorizzare la property snippetName"+loc() :
	!this.snippetName.isNullOrEmpty();
context InlineCodeSnippet if this!=null ERROR "GUI-061: E' necessario valorizzare la property snippetPosition"+loc() :
	!this.snippetPosition.isNullOrEmpty();

context InlineCodeSnippet if (this!=null && !this.lang.isNullOrEmpty()) ERROR "GUI-062: Il valore lang "+this.lang+" non e' valido. Lista valori ammessi "+ getListLangSnippet()+loc():
	!getListLangSnippet().select(e|e == this.lang).isEmpty;
	
context InlineCodeSnippet if (this!=null && !this.snippetPosition.isNullOrEmpty()&& this.eContainer.metaType == IdentityAdapter) 
ERROR "GUI-063: Il valore snippetPosition "+this.snippetPosition+" non e' valido. Lista valori ammessi per IdentityAdapter"+ getListPositionSnippetIdentityAdapter()+loc():
	!getListPositionSnippetIdentityAdapter().select(e|e == this.snippetPosition).isEmpty;
	
context InlineCodeSnippet if (this!=null && !this.snippetName.isNullOrEmpty() && this.eContainer.metaType == IdentityAdapter) 
ERROR "GUI-064: Il valore snippetName "+this.snippetName+" non e' valido. Lista valori ammessi per IdentityAdapter"+ getListNameSnippetIdentityAdapter()+loc() :
	!getListNameSnippetIdentityAdapter().select(e|e == this.snippetName).isEmpty;


context InlineCodeSnippet if (this!=null && !this.snippetPosition.isNullOrEmpty()&& this.eContainer.metaType == PEPImplementation) 
ERROR "GUI-065: Il valore snippetPosition "+this.snippetPosition+" non e' valido. Lista valori ammessi per PEP "+ getListPositionSnippetPEP()+loc():
	!getListPositionSnippetPEP().select(e|e == this.snippetPosition).isEmpty;
	
context InlineCodeSnippet if (this!=null && !this.snippetName.isNullOrEmpty() && this.eContainer.metaType == PEPImplementation) 
ERROR "GUI-066: Il valore snippetName "+this.snippetName+" non e' valido. Lista valori ammessi per PEP "+ getListNameSnippetPEP()+loc() :
	!getListNameSnippetPEP().select(e|e == this.snippetName).isEmpty;


context InlineCodeSnippet if (this!=null && !this.snippetName.isNullOrEmpty() && this.eContainer.metaType == ExecCommand) 
ERROR "GUI-067: Il valore snippetName "+this.snippetName+" non e' valido. Lista valori ammessi per ExecCommand "+ getListNameSnippetExecCmd()+loc() :
	!getListNameSnippetExecCmd().select(e|e == this.snippetName).isEmpty;

context InlineCodeSnippet if (this!=null && !this.snippetPosition.isNullOrEmpty()&& this.eContainer.metaType == ExecCommand) 
ERROR "GUI-068: Il valore snippetPosition "+this.snippetPosition+" non e' valido. Lista valori ammessi per ExecCommnad "+ getListPositionSnippetExecCmd()+loc():
	!getListPositionSnippetExecCmd().select(e|e == this.snippetPosition).isEmpty;
	
context ContentPanel 
ERROR "GUI-069: Non e' (piu') possibile definire un ContentPanel non inserito in un AppModule:"+loc()+", spostarlo in un appmodule (anche interno al modello principale)" :
	this.eContainer.metaType==AppModule;
	
///// check temporanei
//context DataWidget 
//if dataType!=null && dataType.metaType==SimpleType && !((SimpleType)dataType).userDefined
//&& (
//	((SimpleType)dataType).code == SimpleTypeCodes::INT ||
//	((SimpleType)dataType).code == SimpleTypeCodes::FLOAT ||
//	((SimpleType)dataType).code == SimpleTypeCodes::LONG ||
//	((SimpleType)dataType).code == SimpleTypeCodes::DOUBLE
//)
//WARNING "Il SimpleType '"+dataType.name+ "' associato al widget ["+this.metaType.name+":"+name+"] non e' supportato da questa versione, a causa di un malfunzionamento introdotto dalla versione correntemente in uso di struts2 (v. rel. notes della v. 3.1.0). "+
//"Utilizzare il corrispondente tipo 'wrapped':"+loc() :
// ((SimpleType)dataType).nillable == true;
	